# Birdscanner v2
# Last modified: tis okt 28, 2025  04:32
# Sign: JN

configfile: "config/config.yaml"

# Local rules are run on the submit node on a cluster
#localrules: all, OO6_get_scaffold_ids, OO7_get_reference_ids, OO8_select_scaffolds, O11_select_hmms
localrules: all

# Input files
REFERENCES, = glob_wildcards("data/references/{ref}.fas")
GENOMES, = glob_wildcards("data/genomes/{genome}.gz")

# Function for providing list of reference files
def convert_input(wildcards):
    files = expand("data/references/{ref}.fas", ref=REFERENCES)
    return files

# 0. First rule
rule all:
    """
    TODO: Figure out which lines that should be kept
    """
    input:
        expand("results/genomes/{genome}", genome=GENOMES),
        expand("results/hmmer/{genome}.hmmer.out.gz", genome=GENOMES),
        "run/tmp/genes.done"

# 1. Convert fasta
rule OO1_convert_fas:
    """
    Relabel fasta headers to '>filename__i', where i is an iterator. The '__' is important and used later in rule get_reference_ids.
    """
    input:
        "data/references/{ref}.fas"
    output:
        temp("run/tmp/{ref}.fasta")
    shell:
        "awk -v a=$(basename {input} .fas) '/>/{{$0=\">\"a\"__\"++i}}1' {input} > {output}"

# 2. fasta_to_stockholm
rule OO2_fasta_to_stockholm:
    """
    Fasta to Stockholm MSA conversion.
    """
    input:
        "data/references/{ref}.fas"
    output:
        temp("run/tmp/{ref}.sto")
    conda:
        "envs/perl.yaml"
    shell:
        "perl workflow/scripts/bs2-fas-to-sto.pl {input} > {output}"

# 3. create_hmms
rule OO3_create_hmms:
    """
    Create hmms from Stockholm format.
    """
    input:
        "run/tmp/{ref}.sto"
    output:
        temp("run/tmp/{ref}.hmm")
    params:
        hmmbuildtype = config["type"]["hmmbuildtype"]
    conda:
        "envs/hmmer.yaml"
    shell:
        "hmmbuild {params.hmmbuildtype} {output} {input}"

# 4. cat_reference_fas
rule OO4_cat_reference_fas:
    """
    Concatenate all reference fasta files to one (reference.fas).
    """
    input:
        expand("run/tmp/{ref}.fasta", ref=REFERENCES)
    output:
        temp("run/tmp/reference.fas")
    shell:
        "find run/tmp -type f -name \*.fasta -exec cat {{}} \+ > {output}"

# 5. split_genome_fasta
rule OO5_split_genome_fasta:
    """
    Split fasta sequences longer than length=100,000. Note that 100,000 is the maximum length for plast.
    """
    input: 
        "data/genomes/{genome}.gz"
    output: 
        temp("run/plast/{genome}.split.fas")
    threads:
        config["pigz"]["threads"]
    params:
        length = config["splitfast"]["max"]
    conda:
        "envs/pigz.yaml"
    shell:
        "splitfast -m {params.length} <(pigz -p {threads} -d -c {input}) > {output}"

# 6. make_genome_plast_db
rule OO6_make_genome_plast_db:
    """
    Make plast database.
    """
    input:
        "run/plast/{genome}.split.fas",
    output:
        temp(touch("run/tmp/{genome}.makeblastdb.done"))
    params:
        dbtype = config["type"]["dbtype"],
        parse_seqids = config["makeblastdb"]["parseseqids"]
    conda:
        "envs/blast.yaml"
    shell:
        "makeblastdb -in {input} -dbtype {params.dbtype} {params.parse_seqids}"

# 7. run_plast
rule OO7_run_plast:
    """
    Run plast with all concatenated reference sequences as query,
    and the splitted genome fasta as data base.
    """
    input:
        db_done = "run/tmp/{genome}.makeblastdb.done",
        query = "run/tmp/reference.fas"
    output:
        temp("run/plast/{genome}.plast.tab")
    threads:
        config["plast"]["threads"]
    params:
        maxhitperquery = config["plast"]["maxhitperquery"],
        bargraph = config["plast"]["bargraph"],
        plastprog = config["type"]["plastprog"]
    shell:
        "plast -p {params.plastprog} -i {input.query} -d run/plast/{wildcards.genome}.split.fas -o {output} -a {threads} -max-hit-per-query {params.maxhitperquery} {params.bargraph}"

# 8. get_scaffold_ids
rule OO8_get_scaffold_ids:
    """
    The idea is that for each species, we would expect one best hit, and we wish to see which of the scaffolds that most often provides the best hit.
    We wish to search with hmms only against the scaffolds having sufficiently long hits in the plast search.
    Plast outfmt 1:
    query ID, subject ID, percent identities, alignment length, nb. misses, nb. gaps, query begin, query end, subject begin, subject end, e-value, bit score
    We will extract hits where ('alignment length' > minlen), and then we first sort the table on query ID, then sort the table on 'bit score' in descending order, and finally go down
    the list and keep unique 'subject ID'. Note: There is room here to include a scaffold only if having some bit score or e-value.
    """
    input:
        "run/plast/{genome}.plast.tab"
    output:
        temp("run/tmp/{genome}.scaffolds.ids")
    params:
        minlen = config["lengthfilter"]["min"]
    shell:
        """
        awk '$4>{params.minlen}' {input} | \
        sort -t$'\t' -k1,1 -k12rg | \
        awk -F $'\t' '!x[$1]++' | \
        awk -F $'\t' '{{print $2}}' | \
        sort -u > {output}
        """

# 9. get_reference_ids
rule OO9_get_reference_ids:
    """
    Get IDs (file names) for those reference gene files that have a best plast hit with length above minlen.
    NOTE: Room here for more filtering if needed.
    Note: The awk step is dependent on finding the '__', which was introduced in rule convert_fas, to work.
    """
    input:
        "run/plast/{genome}.plast.tab"
    output:
        temp("run/tmp/{genome}.ref.ids")
    params:
        minlen = config["lengthfilter"]["min"]
    shell:
        """
        awk '$4>{params.minlen}' {input} | \
        sort -t$'\t' -k1g -k12rg | \
        awk -F $'\t' '!x[$1]++' | \
        awk -F $'__' '{{print $1 ".hmm"}}' | \
        sort -u > {output}
        """

# 10. select_scaffolds
rule O10_select_scaffolds:
    """
    Extract the scaffolds with best plast hits from the splitted genome fasta.
    """
    input:
        db = "run/plast/{genome}.split.fas",
        idfile = "run/tmp/{genome}.scaffolds.ids"
    output:
        temp("run/plast/{genome}.plast.fas")
    params:
        dbtype = config["type"]["dbtype"],
        outfmt = config["blastdbcmd"]["outfmt"]
    conda:
        "envs/blast.yaml"
    shell:
        "blastdbcmd -db {input.db} -dbtype {params.dbtype} -entry_batch {input.idfile} -outfmt {params.outfmt} -out {output}"

# 11. select_hmms
rule O11_select_hmms:
    """
    Select those hmm from tmp files that have matches in plast, and concatenate them.
    """
    input:
        refids = "run/tmp/{genome}.ref.ids",
        hmms = expand("run/tmp/{ref}.hmm", ref=REFERENCES)
    output:
        temp("run/hmmer/{genome}.hmm")
    shell:
        """
        while IFS= read -r line; do cat run/tmp/$line >> {output}; done < {input.refids}
        """

# 12. run_hmmpress
rule O12_run_hmmpress:
    """
    Run hmmpress.
    """
    input:
        "run/hmmer/{genome}.hmm"
    output:
        temp(touch("run/tmp/{genome}.hmmpress.done"))
    conda:
        "envs/hmmer.yaml"
    shell:
        "hmmpress {input}"

# 13. run_hmmer
rule O13_run_hmmer:
    """
    Run hmmer: the selected scaffolds against the selected hmms.
    """
    input:
        "run/tmp/{genome}.hmmpress.done",
        hmm = "run/hmmer/{genome}.hmm",
        query = "run/plast/{genome}.plast.fas"
    output:
        temp("run/hmmer/{genome}.hmmer.out")
    params:
        hmmerprog = config["type"]["hmmerprog"],
        outfmt = config["hmmer"]["outfmt"],
        hmmercpu = config["hmmer"]["hmmercpu"],
    threads:
        config["hmmer"]["threads"]
    conda:
        "envs/hmmer.yaml"
    shell:
        "{params.hmmerprog} {params.outfmt} --cpu {params.hmmercpu} --tblout {output} {input.hmm} {input.query} > /dev/null"

# 14. parse_hmmer
rule O14_parse_hmmer:
    """
    Parse hmmer output.
    """
    input:
        hmmer = "run/hmmer/{genome}.hmmer.out",
        fas = "run/plast/{genome}.plast.fas"
    output:
        directory("results/genomes/{genome}")
    params:
        prefix = "{genome}",
        fastaheader = "{genome}",
        stats = config["parsehmmer"]["stats"]
    conda:
        "envs/perl.yaml"
    shell:
        "perl workflow/scripts/bs2-parse-nhmmer.pl -f {params.fastaheader} -p {params.prefix} -i {input.hmmer} -g {input.fas} -d {output} {params.stats}"

# 15. gather_genes
rule O15_gather_genes:
    """
    Gather genes from parsed nhmmer output in genome folders.
    """
    input:
        expand("results/genomes/{genome}", genome=GENOMES)
    output:
        dir = directory("results/genes"),
        file = temp(touch("run/tmp/genes.done"))
    conda:
        "envs/perl.yaml"
    shell:
        "perl workflow/scripts/bs2-gather-genes.pl --outdir={output.dir} $(find results/genomes -mindepth 1  -type d)"

# 16. compress_hmmer_out
rule O16_compress_hmmer_out:
    """
    Compress  and keep hmmer output.
    """
    input:
        "run/hmmer/{genome}.hmmer.out"
    output:
        "results/hmmer/{genome}.hmmer.out.gz"
    threads:
        config["pigz"]["threads"]
    conda:
        "envs/pigz.yaml"
    shell:
        "pigz -p {threads} -c {input} > {output}"

# 17. Remove output, keep results
rule clean:
    """
    Remove run directory.
    """
    shell:
        "rm -rf run/"

# 18. Remove all output, including results.
rule distclean:
    """
    Remove run and results directories.
    """
    shell:
        "rm -rf run/ results/"

# 19. When finished, remove run/ and .snakemake_timestamp from results/ when finished
onsuccess:
    """
    Remove run directory on succesful finish.
    """
    shell("rm -r run ; find ./results -type f -name .snakemake_timestamp -exec rm {{}} +")

